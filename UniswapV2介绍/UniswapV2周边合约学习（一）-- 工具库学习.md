# UniswapV2å‘¨è¾¹åˆçº¦å­¦ä¹ ï¼ˆä¸€ï¼‰-- å·¥å…·åº“å­¦ä¹ 

>è®°å¾—æœ‹å‹åœˆçœ‹åˆ°è¿‡ä¸€å¥è¯ï¼Œå¦‚æœDefiæ˜¯ä»¥å¤ªåŠçš„çš‡å† ï¼Œé‚£ä¹ˆUniswapå°±æ˜¯è¿™é¡¶çš‡å† ä¸­çš„æ˜ç ã€‚Uniswapç›®å‰å·²ç»æ˜¯V2ç‰ˆæœ¬ï¼Œç›¸å¯¹V1ï¼Œå®ƒçš„åŠŸèƒ½æ›´åŠ å…¨é¢ä¼˜åŒ–ï¼Œç„¶è€Œå…¶åˆçº¦æºç å´å¹¶ä¸å¤æ‚ã€‚æœ¬æ–‡ä¸ºä¸ªäººå­¦ä¹ UniswapV2æºç çš„ç³»åˆ—è®°å½•æ–‡ç« ã€‚

UniswapV2çš„å‘¨è¾¹åˆçº¦ä¸»è¦ç”¨åšå¤–éƒ¨è´¦å·å’Œæ ¸å¿ƒåˆçº¦ä¹‹é—´çš„æ¡¥æ¢ï¼Œä¹Ÿå°±æ˜¯ç”¨æˆ· => å‘¨è¾¹åˆçº¦ => æ ¸å¿ƒåˆçº¦ã€‚UniswapV2å‘¨è¾¹åˆçº¦ä¸»è¦åŒ…å«æ¥å£å®šä¹‰ï¼Œå·¥å…·åº“ã€Routerå’Œç¤ºä¾‹å®ç°è¿™å››éƒ¨åˆ†ï¼Œ è¿™æ¬¡æˆ‘ä»¬å…ˆæ¥å­¦ä¹ å®ƒçš„å·¥å…·åº“ã€‚

UniswapV2å‘¨è¾¹åˆçº¦çš„å·¥å…·åº“åŒ…å«ä¸¤ä¸ªéƒ¨åˆ†ï¼Œä¸€éƒ¨åˆ†æ˜¯ç›´æ¥å†™åœ¨é¡¹ç›®é‡Œçš„ï¼Œæœ‰ä¸‰ä¸ªåˆçº¦ï¼šSafeMathï¼ŒUniswapV2Libraryå’ŒUniswapV2OracleLibraryã€‚å¦å¤–ä¸€éƒ¨åˆ†æ˜¯Node.jsä¾èµ–åº“ï¼Œéœ€è¦ä½¿ç”¨yarnå®‰è£…çš„ï¼Œä¹ŸåŒ…å«å‡ ä¸ªåº“ã€‚è¿™å…¶ä¸­SafeMathå°±æ˜¯ç®€å•çš„é˜²æº¢å‡ºåº“ï¼Œåœ¨å‰é¢çš„ç³»åˆ—å­¦ä¹ ä¸­å·²ç»è®²è¿‡ï¼Œè¿™é‡Œä¸å†å­¦ä¹ ç ”ç©¶ã€‚

å»ºè®®è¯»è€…åœ¨å¼€å§‹å­¦ä¹ ä¹‹å‰é˜…è¯»æˆ‘çš„å¦ä¸€ç¯‡æ–‡ç« ï¼šUniswapV2ä»‹ç» æ¥å¯¹UniswapV2çš„æ•´ä½“æœºåˆ¶æœ‰ä¸ªå¤§è‡´äº†è§£ï¼Œè¿™æ ·æ›´æœ‰åŠ©äºç†è§£æºç ã€‚

## ä¸€ã€UniswapV2Library
### 1.1ã€æºç 
è¯¥åº“çš„æºç ä¹Ÿåªæœ‰82è¡Œï¼Œç›¸å¯¹æ¯”è¾ƒç®€å•ï¼Œç…§ä¾‹å…ˆè´´æºç ï¼š

```
pragma solidity >=0.5.0;

import '@uniswap/v2-core/contracts/interfaces/IUniswapV2Pair.sol';

import "./SafeMath.sol";

library UniswapV2Library {
    using SafeMath for uint;

    // returns sorted token addresses, used to handle return values from pairs sorted in this order
    function sortTokens(address tokenA, address tokenB) internal pure returns (address token0, address token1) {
        require(tokenA != tokenB, 'UniswapV2Library: IDENTICAL_ADDRESSES');
        (token0, token1) = tokenA < tokenB ? (tokenA, tokenB) : (tokenB, tokenA);
        require(token0 != address(0), 'UniswapV2Library: ZERO_ADDRESS');
    }

    // calculates the CREATE2 address for a pair without making any external calls
    function pairFor(address factory, address tokenA, address tokenB) internal pure returns (address pair) {
        (address token0, address token1) = sortTokens(tokenA, tokenB);
        pair = address(uint(keccak256(abi.encodePacked(
                hex'ff',
                factory,
                keccak256(abi.encodePacked(token0, token1)),
                hex'96e8ac4277198ff8b6f785478aa9a39f403cb768dd02cbee326c3e7da348845f' // init code hash
            ))));
    }

    // fetches and sorts the reserves for a pair
    function getReserves(address factory, address tokenA, address tokenB) internal view returns (uint reserveA, uint reserveB) {
        (address token0,) = sortTokens(tokenA, tokenB);
        (uint reserve0, uint reserve1,) = IUniswapV2Pair(pairFor(factory, tokenA, tokenB)).getReserves();
        (reserveA, reserveB) = tokenA == token0 ? (reserve0, reserve1) : (reserve1, reserve0);
    }

    // given some amount of an asset and pair reserves, returns an equivalent amount of the other asset
    function quote(uint amountA, uint reserveA, uint reserveB) internal pure returns (uint amountB) {
        require(amountA > 0, 'UniswapV2Library: INSUFFICIENT_AMOUNT');
        require(reserveA > 0 && reserveB > 0, 'UniswapV2Library: INSUFFICIENT_LIQUIDITY');
        amountB = amountA.mul(reserveB) / reserveA;
    }

    // given an input amount of an asset and pair reserves, returns the maximum output amount of the other asset
    function getAmountOut(uint amountIn, uint reserveIn, uint reserveOut) internal pure returns (uint amountOut) {
        require(amountIn > 0, 'UniswapV2Library: INSUFFICIENT_INPUT_AMOUNT');
        require(reserveIn > 0 && reserveOut > 0, 'UniswapV2Library: INSUFFICIENT_LIQUIDITY');
        uint amountInWithFee = amountIn.mul(997);
        uint numerator = amountInWithFee.mul(reserveOut);
        uint denominator = reserveIn.mul(1000).add(amountInWithFee);
        amountOut = numerator / denominator;
    }

    // given an output amount of an asset and pair reserves, returns a required input amount of the other asset
    function getAmountIn(uint amountOut, uint reserveIn, uint reserveOut) internal pure returns (uint amountIn) {
        require(amountOut > 0, 'UniswapV2Library: INSUFFICIENT_OUTPUT_AMOUNT');
        require(reserveIn > 0 && reserveOut > 0, 'UniswapV2Library: INSUFFICIENT_LIQUIDITY');
        uint numerator = reserveIn.mul(amountOut).mul(1000);
        uint denominator = reserveOut.sub(amountOut).mul(997);
        amountIn = (numerator / denominator).add(1);
    }

    // performs chained getAmountOut calculations on any number of pairs
    function getAmountsOut(address factory, uint amountIn, address[] memory path) internal view returns (uint[] memory amounts) {
        require(path.length >= 2, 'UniswapV2Library: INVALID_PATH');
        amounts = new uint[](path.length);
        amounts[0] = amountIn;
        for (uint i; i < path.length - 1; i++) {
            (uint reserveIn, uint reserveOut) = getReserves(factory, path[i], path[i + 1]);
            amounts[i + 1] = getAmountOut(amounts[i], reserveIn, reserveOut);
        }
    }

    // performs chained getAmountIn calculations on any number of pairs
    function getAmountsIn(address factory, uint amountOut, address[] memory path) internal view returns (uint[] memory amounts) {
        require(path.length >= 2, 'UniswapV2Library: INVALID_PATH');
        amounts = new uint[](path.length);
        amounts[amounts.length - 1] = amountOut;
        for (uint i = path.length - 1; i > 0; i--) {
            (uint reserveIn, uint reserveOut) = getReserves(factory, path[i - 1], path[i]);
            amounts[i - 1] = getAmountIn(amounts[i], reserveIn, reserveOut);
        }
    }
}
```

### 1.2ã€å­¦ä¹ 

é¦–å…ˆï¼Œæˆ‘ä»¬è¦æ³¨æ„åˆ°è¯¥åº“ä¸­æ‰€æœ‰çš„å‡½æ•°éƒ½æ˜¯internalç±»å‹çš„ã€‚ä¸ºä»€ä¹ˆå‘¢ï¼Œå› ä¸ºæ‰€æœ‰å¤–éƒ¨åº“å‡½æ•°è°ƒç”¨éƒ½æ˜¯çœŸå®çš„EVMå‡½æ•°è°ƒç”¨ï¼Œå®ƒä¼šæœ‰é¢å¤–çš„å¼€é”€ ã€‚å½“ç„¶å¤–éƒ¨åº“å‡½æ•°è°ƒç”¨çš„å‚æ•°ç±»å‹æ›´å¹¿æ³›ã€‚

- ç¬¬ä¸€è¡Œç”¨æ¥æŒ‡å®šSolidityç‰ˆæœ¬é«˜äºæˆ–è€…ç­‰äº0.5.0
- ç¬¬äºŒè¡Œç”¨æ¥å¯¼å…¥IUniswapV2Pair.solï¼Œä¹Ÿå°±æ˜¯äº¤æ˜“å¯¹çš„æ¥å£ï¼Œæ³¨æ„å®ƒæ˜¯ä½¿ç”¨Node.jsçš„moduleå¯¼å…¥çš„ã€‚
- ç¬¬ä¸‰è¡Œå¯¼å…¥SafeMathï¼Œæ³¨æ„å®ƒæ˜¯æ­£å¸¸ä½¿ç”¨ç›¸å¯¹è·¯å¾„å¯¼å…¥çš„
- ç¬¬å››è¡Œï¼Œlibrary *UniswapV2Library* { åº“å®šä¹‰ã€‚
- ç¬¬äº”è¡Œï¼Œåœ¨Uintç±»å‹ä¸Šä½¿ç”¨SafeMathã€‚
- sortTokenså‡½æ•°ã€‚å¯¹åœ°å€è¿›è¡Œä»å°åˆ°å¤§æ’åºå¹¶éªŒè¯ä¸èƒ½ä¸ºé›¶åœ°å€ã€‚
- pairForå‡½æ•°ã€‚æ³¨é‡Šä¸­å·²ç»æŒ‡å‡ºå®ƒæ˜¯è®¡ç®—ç”Ÿæˆçš„äº¤æ˜“å¯¹çš„åœ°å€çš„ã€‚å…·ä½“è®¡ç®—æ–¹æ³•å¯ä»¥åˆ†ä¸ºé“¾ä¸‹è®¡ç®—å’Œé“¾ä¸Šåˆçº¦è®¡ç®—ã€‚åˆçº¦è®¡ç®—çš„æ–¹æ³•åœ¨å­¦ä¹ æ ¸å¿ƒåˆçº¦factoryæ—¶å·²ç»è®²äº†ã€‚è¿™é‡Œéœ€è¦æ³¨æ„çš„æ˜¯init code hashçš„è®¡ç®—ï¼Œä¹Ÿå¯ä»¥é“¾ä¸Šåˆçº¦è®¡ç®—æˆ–è€…é“¾ä¸‹è®¡ç®—ï¼Œå½“ç„¶é“¾ä¸‹è®¡ç®—æ›´æ–¹ä¾¿ä¸€äº›ã€‚ä½†æ˜¯è¿™é‡Œä¼šæœ‰ä¸ªå°å‘å“Ÿï¼Œé“¾ä¸‹è®¡ç®—æ–¹æ³•åŠå‘æ˜¯ä»€ä¹ˆæˆ‘è¿™é‡Œå–ä¸ªå…³å­å°±ä¸è®²äº†ï¼Œå¤§å®¶æœ‰å…´è¶£çš„å¯ä»¥åœ¨githubä¸Šçœ‹ä¸€ä¸‹Issuesï¼Œè®°å¾—å…³é—­çš„ä¹Ÿè¦çœ‹çš„ï¼Œçœ‹å®Œå°±å¯ä»¥æ˜ç™½äº†ã€‚
- getReserveså‡½æ•°ã€‚è·å–æŸä¸ªäº¤æ˜“å¯¹ä¸­æ’å®šä¹˜ç§¯çš„å„èµ„äº§çš„å€¼ã€‚å› ä¸ºè¿”å›çš„èµ„äº§å€¼æ˜¯æ’åºè¿‡çš„ï¼Œè€Œè¾“å…¥å‚æ•°æ˜¯ä¸ä¼šæœ‰æ’åºçš„ï¼Œæ‰€ä»¥å‡½æ•°çš„æœ€åä¸€è¡Œåšäº†å¤„ç†ã€‚
- quoteå‡½æ•°ã€‚æ ¹æ®æ¯”ä¾‹ç”±ä¸€ç§èµ„äº§è®¡ç®—å¦ä¸€ç§èµ„äº§çš„å€¼ï¼Œå¾ˆå¥½ç†è§£ã€‚
- getAmountOutå‡½æ•°ã€‚A/Bäº¤æ˜“å¯¹ä¸­å–å‡ºAèµ„äº§ï¼Œè®¡ç®—ä¹°è¿›çš„Bèµ„äº§çš„æ•°é‡ã€‚æ³¨æ„ï¼Œå–å‡ºçš„èµ„äº§æ‰£é™¤äº†åƒä¹‹åˆ†ä¸‰çš„äº¤æ˜“æ‰‹ç»­è´¹ã€‚å…¶è®¡ç®—å…¬å¼ä¸ºï¼š
  - åˆå§‹æ¡ä»¶ `A * B = K`
  - äº¤æ˜“åæ¡ä»¶ `( A + A0 ) * ( B - B0 ) = k`
  - è®¡ç®—å¾—åˆ° `B0 = A0 * B / ( A + A0)`
  - è€ƒè™‘åƒåˆ†ä¹‹ä¸‰çš„æ‰‹ç»­è´¹ï¼Œå°†ä¸Šå¼ä¸­çš„ä¸¤ä¸ªA0ä½¿ç”¨997 * A0 /1000ä»£æ›¿ï¼Œæœ€åå¾—åˆ°ç»“æœä¸º B0 = 997 * A0 * B / (1000 * A + 997 * A0 )
- getAmountInå‡½æ•°ã€‚A/Bäº¤æ˜“å¯¹ä¸­ä¹°è¿›Bèµ„äº§ï¼Œè®¡ç®—å–å‡ºçš„Aèµ„äº§çš„æ•°é‡ã€‚æ³¨æ„ï¼Œå®ƒä¹Ÿè€ƒè™‘äº†æ‰‹ç»­è´¹ã€‚å®ƒå’ŒgetAmountOutå‡½æ•°çš„åŒºåˆ«æ˜¯ä¸€ä¸ªæŒ‡å®šå–å‡ºçš„æ•°é‡ï¼Œä¸€ä¸ªæ˜¯æŒ‡å®šä¹°è¿›çš„æ•°é‡ã€‚å› ä¸ºæ˜¯æ’å®šä¹˜ç§¯ç®—æ³•ï¼Œä»·æ ¼æ˜¯éçº¿æ€§çš„ï¼Œæ‰€ä»¥ä¼šæœ‰ä¸¤ç§è®¡ç®—æ–¹å¼ã€‚å…¶è®¡ç®—å…¬å¼ä¸ºï¼š
  - åˆå§‹æ¡ä»¶ A * B = K
  - äº¤æ˜“åæ¡ä»¶ ( A + A0 ) * ( B - B0 ) = k
  - è®¡ç®—å¾—åˆ° A0 = A * B0 / ( B - B0)
  - è€ƒè™‘åƒåˆ†ä¹‹ä¸‰çš„æ‰‹ç»­è´¹ï¼ŒA0 = A0 * 1000 / 997ï¼Œæ‰€ä»¥è®¡ç®—ç»“æœä¸º A0 = A * B0 * 1000 / (( B - B0 ) * 997)
  - å› ä¸ºé™¤æ³•æ˜¯åœ°æ¿é™¤ï¼Œä½†æ˜¯å–è¿›çš„èµ„äº§ä¸èƒ½å°‘ï¼ˆå¯ä»¥å¤šä¸€ç‚¹ï¼‰ï¼Œæ‰€ä»¥æœ€åç»“æœè¿˜éœ€è¦å†åŠ ä¸Šä¸€ä¸ª1ã€‚
- getAmountsOutå‡½æ•°ã€‚è®¡ç®—é“¾å¼äº¤æ˜“ä¸­å–å‡ºæŸèµ„äº§ï¼Œå¾—åˆ°çš„ä¸­é—´èµ„äº§å’Œæœ€ç»ˆèµ„äº§çš„æ•°é‡ã€‚ä¾‹å¦‚ A/B => B/C å–å‡ºAï¼Œå¾—åˆ°BCçš„æ•°é‡ã€‚
- getAmountsInå‡½æ•°ã€‚è®¡ç®—é“¾å¼äº¤æ˜“ä¸­ä¹°è¿›æŸèµ„äº§ï¼Œéœ€è¦å–å‡ºçš„ä¸­é—´èµ„äº§å’Œåˆå§‹èµ„äº§æ•°é‡ã€‚ä¾‹å¦‚ A/B => B/C ä¹°è¿›Cï¼Œå¾—åˆ°ABçš„æ•°é‡ã€‚å› ä¸ºä»ä¹°è¿›æ¨å¯¼å–å‡ºæ˜¯åå‘è¿›è¡Œçš„ï¼Œæ‰€ä»¥æ•°æ®æ˜¯åå‘éå†çš„ã€‚
## äºŒã€UniswapV2OracleLibrary
### 2.1ã€æºç 

è¯¥åº“çš„æºç å¾ˆçŸ­ï¼Œåªæœ‰35è¡Œï¼Œåªæœ‰ä¸¤ä¸ªå‡½æ•°ã€‚

```
pragma solidity >=0.5.0;

import '@uniswap/v2-core/contracts/interfaces/IUniswapV2Pair.sol';
import '@uniswap/lib/contracts/libraries/FixedPoint.sol';

// library with helper methods for oracles that are concerned with computing average prices
library UniswapV2OracleLibrary {
    using FixedPoint for *;

    // helper function that returns the current block timestamp within the range of uint32, i.e. [0, 2**32 - 1]
    function currentBlockTimestamp() internal view returns (uint32) {
        return uint32(block.timestamp % 2 ** 32);
    }

    // produces the cumulative price using counterfactuals to save gas and avoid a call to sync.
    function currentCumulativePrices(
        address pair
    ) internal view returns (uint price0Cumulative, uint price1Cumulative, uint32 blockTimestamp) {
        blockTimestamp = currentBlockTimestamp();
        price0Cumulative = IUniswapV2Pair(pair).price0CumulativeLast();
        price1Cumulative = IUniswapV2Pair(pair).price1CumulativeLast();

        // if time has elapsed since the last update on the pair, mock the accumulated price values
        (uint112 reserve0, uint112 reserve1, uint32 blockTimestampLast) = IUniswapV2Pair(pair).getReserves();
        if (blockTimestampLast != blockTimestamp) {
            // subtraction overflow is desired
            uint32 timeElapsed = blockTimestamp - blockTimestampLast;
            // addition overflow is desired
            // counterfactual
            price0Cumulative += uint(FixedPoint.fraction(reserve1, reserve0)._x) * timeElapsed;
            // counterfactual
            price1Cumulative += uint(FixedPoint.fraction(reserve0, reserve1)._x) * timeElapsed;
        }
    }
}
```

### 2.2ã€å­¦ä¹ 

- ç¬¬ä¸€è¡Œç”¨æ¥æŒ‡å®šSolidityç‰ˆæœ¬é«˜äºæˆ–è€…ç­‰äº0.5.0
- æ¥ä¸‹æ¥ä¸¤è¡Œå¯¼å…¥è¯­å¥åˆ†åˆ«å¯¼å…¥äº¤æ˜“å¯¹åˆçº¦æ¥å£å’Œè‡ªå®šä¹‰çš„æµ®ç‚¹æ•°åº“ã€‚åˆçº¦æ¥å£è§æ ¸å¿ƒåˆçº¦å­¦ä¹ ç›¸å…³æ–‡ç« ï¼Œæµ®ç‚¹æ•°åº“åœ¨ä¸‹é¢ä»‹ç»ã€‚
- æ¥ä¸‹æ¥çš„æ³¨é‡Šé˜è¿°äº†è¯¥åº“çš„ç”¨å¤„ï¼Œè®¡ç®—å½“å‰ç´¯è®¡ä»·æ ¼ï¼ŒåŒæ—¶é¿å…åŒæ­¥è°ƒç”¨ï¼ŒèŠ‚çœæ‰‹ç»­è´¹ã€‚
- library UniswapV2OracleLibrary { åº“å®šä¹‰
- using FixedPoint for *;åœ¨æ‰€æœ‰æ•°æ®ç±»å‹ä¸Šä½¿ç”¨FixedPointåº“ï¼Œä»ä¸­å¯ä»¥çœ‹å‡ºåº“ä¸­ä¹Ÿå¯ä»¥ä½¿ç”¨åˆ«çš„åº“ï¼Œè¯­æ³•æ˜¯ä¸€æ ·çš„ã€‚
- currentBlockTimestampè·å–å½“å‰åŒºå—æ—¶é—´ï¼Œæ³¨æ„è¿™é‡Œå’Œäº¤æ˜“å¯¹åˆçº¦ä¸­çš„å¤„ç†æ–¹å¼ä¸€æ ·ï¼Œå–æ¨¡æ“ä½œã€‚ç„¶è€Œå°±ç®—æº¢å‡ºäº†ï¼Œç›´æ¥è¿›è¡Œç±»å‹è½¬æ¢ä¹Ÿä¼šå¾—åˆ°å’Œå–æ¨¡æ“ä½œç›¸åŒçš„å€¼ã€‚è¿™ä¸ªé—®é¢˜æˆ‘åœ¨æ ¸å¿ƒåˆçº¦å­¦ä¹ ä¸‰ä¸­å·²ç»æ›´æ–°è¿‡äº†ï¼Œå¼€å‘è€…ç»™å‡ºç­”æ¡ˆäº†ã€‚
- currentCumulativePriceså‡½æ•°ã€‚è®¡ç®—å½“å‰åŒºå—ç´¯ç§¯ä»·æ ¼ã€‚å¦‚æœå½“å‰åŒºå—äº¤æ˜“å¯¹åˆçº¦å·²ç»è®¡ç®—è¿‡äº†ï¼ˆä¸¤ä¸ªåŒºå—æ—¶é—´ä¸€è‡´ï¼‰ï¼Œåˆ™è·³è¿‡ï¼›å¦‚æœæ²¡æœ‰ï¼Œåˆ™åŠ ä¸Šå»ã€‚æ³¨æ„å®ƒæ˜¯viewå‡½æ•°ï¼Œå¹¶æœªæ›´æ–°ä»»ä½•çŠ¶æ€å˜é‡ï¼Œè¿™ä¸ªç´¯è®¡å€¼æ˜¯è®¡ç®—å‡ºæ¥çš„ã€‚
- 
## ä¸‰ã€FixedPointåº“

å› ä¸ºUniswapV2OracleLibraryåº“çš„æºç ä¸­ä½¿ç”¨äº†FixedPointåº“ï¼Œæ‰€ä»¥æˆ‘ä»¬é¡ºä¾¿ä¹Ÿå­¦ä¹ ä¸€ä¸‹è¯¥åº“ã€‚æ³¨æ„ï¼Œè¯¥åº“å¹¶ä¸æ˜¯ä»¥ç¼–å†™æºç çš„æ–¹å¼ä¿å­˜ä¸ºæ–‡ä»¶ç›´æ¥å¯¼å…¥çš„ï¼Œè€Œæ˜¯é€šè¿‡Node.jsæ¨¡å—å¯¼å…¥ï¼Œå±äºä¾èµ–åº“ã€‚æŸ¥çœ‹å…¶å‘¨è¾¹åˆçº¦çš„README.mdå¯ä»¥çœ‹åˆ°ï¼Œè¿è¡Œyarnå‘½ä»¤æ¥å®‰è£…æ‰€æœ‰ä¾èµ–ã€‚

### 3.1ã€æºç 

ä¸‹é¢æ˜¯è¯¥åº“çš„æºç ï¼š

```
pragma solidity >=0.4.0;

// a library for handling binary fixed point numbers (https://en.wikipedia.org/wiki/Q_(number_format))
library FixedPoint {
    // range: [0, 2**112 - 1]
    // resolution: 1 / 2**112
    struct uq112x112 {
        uint224 _x;
    }

    // range: [0, 2**144 - 1]
    // resolution: 1 / 2**112
    struct uq144x112 {
        uint _x;
    }

    uint8 private constant RESOLUTION = 112;

    // encode a uint112 as a UQ112x112
    function encode(uint112 x) internal pure returns (uq112x112 memory) {
        return uq112x112(uint224(x) << RESOLUTION);
    }

    // encodes a uint144 as a UQ144x112
    function encode144(uint144 x) internal pure returns (uq144x112 memory) {
        return uq144x112(uint256(x) << RESOLUTION);
    }

    // divide a UQ112x112 by a uint112, returning a UQ112x112
    function div(uq112x112 memory self, uint112 x) internal pure returns (uq112x112 memory) {
        require(x != 0, 'FixedPoint: DIV_BY_ZERO');
        return uq112x112(self._x / uint224(x));
    }

    // multiply a UQ112x112 by a uint, returning a UQ144x112
    // reverts on overflow
    function mul(uq112x112 memory self, uint y) internal pure returns (uq144x112 memory) {
        uint z;
        require(y == 0 || (z = uint(self._x) * y) / y == uint(self._x), "FixedPoint: MULTIPLICATION_OVERFLOW");
        return uq144x112(z);
    }

    // returns a UQ112x112 which represents the ratio of the numerator to the denominator
    // equivalent to encode(numerator).div(denominator)
    function fraction(uint112 numerator, uint112 denominator) internal pure returns (uq112x112 memory) {
        require(denominator > 0, "FixedPoint: DIV_BY_ZERO");
        return uq112x112((uint224(numerator) << RESOLUTION) / denominator);
    }

    // decode a UQ112x112 into a uint112 by truncating after the radix point
    function decode(uq112x112 memory self) internal pure returns (uint112) {
        return uint112(self._x >> RESOLUTION);
    }

    // decode a UQ144x112 into a uint144 by truncating after the radix point
    function decode144(uq144x112 memory self) internal pure returns (uint144) {
        return uint144(self._x >> RESOLUTION);
    }
}
```

### 3.2ã€å­¦ä¹ 

- ç¬¬ä¸€è¡ŒæŒ‡å®šäº†ä½¿ç”¨çš„Solidityç‰ˆæœ¬
- ç¬¬äºŒè¡Œæ˜¯åº“å®šä¹‰ï¼Œæ³¨æ„åº“æ²¡æœ‰ç»§æ‰¿ã€‚
- æ¥ä¸‹æ¥å®šä¹‰ä¸¤ä¸ªæ•°æ®ç»“æ„ï¼Œä¸€ä¸ªæ˜¯uq112x112ï¼ŒåŠ èµ·æ¥å°±æ˜¯224ä½ï¼Œæ‰€ä»¥å®ƒçš„å­—æ®µåªæœ‰ä¸€ä¸ªuint224çš„_xã€‚ä¸€ä¸ªæ˜¯uq144x112ï¼ŒåŠ èµ·æ¥å°±æ˜¯256ä½ï¼Œæ‰€ä»¥å®ƒçš„å­—æ®µä¸ºuintçš„_xã€‚æ³¨æ„å®ƒçš„æ³¨é‡Šåˆ†åˆ«ä»£è¡¨å–å€¼èŒƒå›´å’Œç²¾åº¦ï¼ˆå°æ•°ï¼‰ã€‚
- uint8 private constant RESOLUTION = 112;å®šä¹‰ä¸åŒå¤§å°æ•°æ®è½¬æ¢æ—¶å·¦ç§»æˆ–è€…å³ç§»çš„ä½æ•°ã€‚
- encodeå‡½æ•°ã€‚å°†uint112è½¬æˆuq112x112ç»“æ„ã€‚
- encode144å‡½æ•°ã€‚å°†uint112è½¬æˆuq114x112ç»“æ„ã€‚
- divå‡½æ•°ã€‚ä¸€ä¸ªuq112x112ç±»å‹é™¤äºä¸€ä¸ªuint112ï¼Œæ³¨æ„å…ˆuint112è½¬åŒ–æˆäº†uint224ï¼Œç»“æœä¹Ÿæ˜¯ä¸€ä¸ªuq112x112ã€‚ï¼ˆä¸¤ä¸ª112ä½åˆ†åˆ«ä»£è¡¨æ•°å€¼å’Œç²¾åº¦ï¼‰ã€‚
- mulå‡½æ•°ã€‚å°†ä¸€ä¸ªuq112x112å’Œä¸€ä¸ªuintç›¸ä¹˜æ³•ã€‚æ³¨æ„ï¼Œåšäº†é˜²æº¢å‡ºå¤„ç†ï¼Œç»“æœæ˜¯ä¸€ä¸ªuq144x112ï¼Œç›¸æ¯”uq112x112ï¼Œæœ€å·¦è¾¹çš„32ä½æ˜¯ä¿å­˜çš„ç›¸å¯¹uint224çš„æº¢å‡ºä½ã€‚
- fractionå‡½æ•°ï¼Œç”¨æ¥åœ¨ä¸¤ä¸ªuint112ç›¸é™¤æ—¶æé«˜ç²¾åº¦ï¼Œå°†åˆ†å­å·¦ç§»112ä½ï¼Œé‚£ä¹ˆç»“æœçš„å·¦è¾¹112ä½å°±æ˜¯å€¼ï¼Œå³è¾¹çš„112ä½ç›¸å½“äºå°æ•°ä½ã€‚ç”¨äºUniswapV2çš„ä»·æ ¼è®¡ç®—å½“ä¸­ã€‚
- decodeå‡½æ•°ï¼Œå°†ä¸€ä¸ªuq112x112ï¼ˆuint224ï¼‰å³ç§»112ä½å¹¶å°†ç»“æœè½¬æ¢æˆuint112ï¼Œç›¸å½“äºå³è¾¹112ä½å°æ•°ä½è¢«æˆªæ–­äº†ã€‚
- decode144å‡½æ•°ï¼ŒåŒä¸Šï¼Œåªæ˜¯æ•°æ®ç±»å‹å˜æˆäº†uq144x112ï¼ˆuint256)ã€‚

å› ä¸ºæœ¬åº“ä¸»è¦åŠŸèƒ½æ˜¯æé«˜ä»·æ ¼è®¡ç®—æ—¶çš„ç²¾åº¦ï¼Œåœ¨UniswapV2å‘¨è¾¹åˆçº¦ä¸­ï¼Œè¯¥åº“çš„ç»å¤§éƒ¨åˆ†å‡½æ•°ä»…åœ¨é¢„è¨€æœºç¤ºä¾‹åˆçº¦ä¸­ä½¿ç”¨ã€‚

## å››ã€TransferHelperåº“

æœ‰ä¸ªç®€å•çš„åº“ä¹Ÿè¦æä¸€ä¸‹ï¼Œå®ƒå°±æ˜¯TransferHelperåº“ï¼Œå®ƒä¹Ÿæ˜¯é€šè¿‡ä¾èµ–å®‰è£…å¯¼å…¥çš„ã€‚ä¸»è¦ç›®çš„æ˜¯ç”¨æ¥ç»Ÿä¸€å¤„ç†æ ‡å‡†ERC20ä»£å¸å’Œéæ ‡å‡†ERC20ä»£å¸ä¹‹é—´éƒ¨åˆ†å‡½æ•°çš„è¿”å›å€¼é—®é¢˜ï¼ˆä¸»è¦æ˜¯è½¬ç§»ä»£å¸å’Œæˆæƒçš„è¿”å›å€¼ï¼‰ã€‚å®ƒé€šè¿‡ä½¿ç”¨ä¸€ä¸ªä½çº§çš„callå‡½æ•°è°ƒç”¨æ¥ä»£æ›¿æ­£å¸¸çš„åˆçº¦è°ƒç”¨ï¼Œå¹¶å¯¹æ‰§è¡Œç»“æœå’Œè¿”å›å€¼åšå¤„ç†ã€‚è¿™æ ·å¤„ç†çš„ç›®çš„è§UniswapV2ä»‹ç»ã€‚

æ³¨æ„ï¼šä½¿ç”¨callè°ƒç”¨åˆçº¦å¿…é¡»æä¾›å‡½æ•°çš„é€‰æ‹©å™¨ï¼ˆå¦‚æœå­˜åœ¨ï¼‰ï¼Œè®¡ç®—æ–¹å¼æ³¨é‡Šä¸­å·²ç»å†™æ˜äº†ã€‚

### 4.1ã€æºç 

```
pragma solidity >=0.6.0;

// helper methods for interacting with ERC20 tokens and sending ETH that do not consistently return true/false
library TransferHelper {
    function safeApprove(address token, address to, uint value) internal {
        // bytes4(keccak256(bytes('approve(address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0x095ea7b3, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: APPROVE_FAILED');
    }

    function safeTransfer(address token, address to, uint value) internal {
        // bytes4(keccak256(bytes('transfer(address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0xa9059cbb, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: TRANSFER_FAILED');
    }

    function safeTransferFrom(address token, address from, address to, uint value) internal {
        // bytes4(keccak256(bytes('transferFrom(address,address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0x23b872dd, from, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: TRANSFER_FROM_FAILED');
    }

    function safeTransferETH(address to, uint value) internal {
        (bool success,) = to.call{value:value}(new bytes(0));
        require(success, 'TransferHelper: ETH_TRANSFER_FAILED');
    }
}
```
### 4.2ã€å­¦ä¹ 

- safeApproveæˆæƒå‡½æ•°ã€‚è¢«è°ƒç”¨å‡½æ•°å¯ä»¥æœ‰è¿”å›å€¼ï¼ˆä¸ºtrue)æˆ–è€…æ— è¿”å›å€¼ï¼Œå‡ä¼šè¢«è§†ä¸ºæˆåŠŸã€‚
- safeTransferç›´æ¥è½¬ç§»ä»£å¸å‡½æ•°ï¼Œè¿”å›å€¼å¤„ç†åŒä¸Šã€‚
- safeTransferFromæˆæƒè½¬ç§»ä»£å¸å‡½æ•°ï¼Œè¿”å›å€¼å¤„ç†åŒä¸Šã€‚
- safeTransferETHå‘é€ETHï¼Œæ³¨æ„ç­‰å¼å³è¾¹çš„è¯­æ³•ï¼š(bool success,) = to.call{value:value}(new bytes(0));ã€‚valueä»£è¡¨å‘é€çš„ETHæ•°é‡ï¼ˆå•ä½ä¸ºwei)ï¼Œnew bytes(0)ä»£è¡¨ä¸ºç©ºæ•°æ®payloadã€‚

æ³¨æ„ï¼šè¿™é‡Œæœ‰ç‚¹å°ç‘•ç–µã€‚è™½ç„¶æœ¬åº“ä»£ç ç¬¬ä¸€è¡ŒæŒ‡å®šäº†Solidityç‰ˆæœ¬ä¸º>=0.6.0ï¼Œä½†æ˜¯(bool success,) = to.call{value:value}(new bytes(0));ä½¿ç”¨çš„è¯­æ³•åœ¨0.6.2ç‰ˆæœ¬æ‰èƒ½ç¼–è¯‘é€šè¿‡ã€‚ä¸è¿‡å•ç‹¬çœ‹æœ‰è¿™ä¹ˆä¸€ç‚¹å°é—®é¢˜ï¼Œä½†æ˜¯å› ä¸ºä½¿ç”¨è¯¥åº“çš„åˆçº¦æºç å‡æŒ‡å®šSolidityç‰ˆæœ¬ä¸º0.6.6ï¼Œæ‰€ä»¥è”åˆä½¿ç”¨èµ·æ¥ä½¿ç”¨æ²¡æœ‰é—®é¢˜ã€‚å½“ç„¶å¦‚æœèƒ½å°†pragma solidity >=0.6.0;æ¢æˆpragma solidity >=0.6.2;å°±æ›´ç²¾ç¡®äº†ã€‚

## äº”ã€å…¶å®ƒä¾èµ–

node_modules/@uniswap/lib/contracts/libraries/ç›®å½•ä¸‹è¿˜æœ‰å…¶å®ƒä¸€äº›ä¾èµ–åº“ï¼Œä¸»è¦æ˜¯è¿›è¡Œä¸€äº›å­—ç¬¦ä¸²æˆ–è€…å­—ç¬¦æ“ä½œï¼Œè¿™é‡Œå°±ä¸ä¸€ä¸€å­¦ä¹ äº†ã€‚éœ€è¦æåˆ°ä¸€ç‚¹çš„æ˜¯åœ¨PairNamer.solæºç ä¸­ï¼Œå‡ºç°äº†string private constant TOKEN_SYMBOL_PREFIX = 'ğŸ¦„';é‚£ä¸ªç‹¬è§’å…½å›¾æ ‡å…¶å®æ˜¯ä¸€ä¸ªUnicodeå­—ç¬¦ã€‚åœ¨Solidityä¸­ï¼Œå­—ç¬¦ä¸²å­—é¢å€¼æ˜¯æ”¯æŒunicodeçš„ã€‚ğŸ¦„å­—ç¬¦ä»UniswapV1èµ·å¼€å§‹ä½¿ç”¨ï¼Œå®ƒçš„è¯¦ç»†è¯´æ˜ç½‘å€ä¸º:https://emojipedia.org/unicorn/ã€‚å½“ç„¶å¦‚æœä½ æ„¿æ„ï¼Œå¯ä»¥æŒ‘é€‰ä¸€ä¸ªä½ å–œæ¬¢çš„å…¶å®ƒUnicodeå­—ç¬¦æ¥æ›¿æ¢å®ƒã€‚

ä¸è¿‡è¿™é‡ŒåŒæ ·å­˜åœ¨ç¼–è¯‘å™¨ç‰ˆæœ¬çš„é—®é¢˜ï¼Œåœ¨PairNamer.solæºç ä¸­ï¼Œç»™å‡ºäº†pragma solidity >=0.5.0;ã€‚ä½†å®é™…ä¸Šåœ¨0.7.0åï¼Œåœ¨æœ‰æ•ˆçš„UTF-8åºåˆ—ä¸­æ’å…¥Unicodeå­—ç¬¦éœ€è¦å¢åŠ unicodeå‰ç¼€ï¼Œä¾‹å¦‚ï¼š

`string memory a = unicode"Hello ğŸ˜ƒ";`

UniswapV2æœªä½¿ç”¨Solidity 0.7.0ä»¥ä¸Šç‰ˆæœ¬ï¼Œæ‰€ä»¥è¿™é‡Œä¸éœ€è¦ã€‚å¦‚æœä½¿ç”¨åŠ unicodeçš„æ–°è¯­æ³•ï¼ŒSolidityç‰ˆæœ¬å¿…é¡»0.7.0ä»¥ä¸Šã€‚

æœ€åä¸€ç‚¹ï¼Œåº“å…¶å®åªç”¨éƒ¨ç½²ä¸€æ¬¡ï¼Œåœ¨ç¼–è¯‘æ—¶å°†å®ƒçš„åœ°å€é“¾æ¥åˆ°ä½¿ç”¨çš„åˆçº¦å³å¯ï¼ˆä½¿ç”¨ä¸€äº›å·¥å…·è‡ªåŠ¨éƒ¨ç½²æ—¶çœ‹ä¸å‡ºæ¥ï¼Œå¯ä»¥ä½¿ç”¨truffleè¿›è¡Œæ‰‹åŠ¨éƒ¨ç½²åº“å†è¿›è¡Œé“¾æ¥ï¼‰ã€‚ä½†æ˜¯åº“æºç ä¸€èˆ¬éƒ½ä¸å¤§ï¼Œä¸€ä¸ªæ–°é¡¹ç›®åŸºæœ¬ä¸Šéƒ½ä¼šé‡æ–°éƒ¨ç½²ä¸€ä¸ªç›¸åŒçš„åº“ï¼ˆä¾‹å¦‚SafeMathï¼‰ï¼Œè€Œä¸ä¼šé‡ç”¨ä»¥å‰éƒ¨ç½²å¥½çš„åº“ã€‚
